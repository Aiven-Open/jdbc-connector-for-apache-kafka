/*
 * Copyright 2020 Aiven Oy
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    // https://docs.gradle.org/current/userguide/java_library_plugin.html
    id "java-library"

    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    id "checkstyle"

    // https://docs.gradle.org/current/userguide/jacoco_plugin.html
    id "jacoco"

    // https://docs.gradle.org/current/userguide/distribution_plugin.html
    id "distribution"

    // https://docs.gradle.org/current/userguide/publishing_maven.html
    id "maven-publish"

    // https://docs.gradle.org/current/userguide/idea_plugin.html
    id "idea"
}

repositories {
    jcenter()
    // For kafka-avro-serializer and kafka-connect-avro-converter
    maven {
        url "https://packages.confluent.io/maven"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

checkstyle {
    toolVersion "8.35"
    configDir rootProject.file("checkstyle/")
    println configFile
}

jacoco {
    toolVersion = "0.8.5"
}

distributions {
    main {
        contents {
            from jar
            from configurations.runtimeClasspath

            into("/") {
                from projectDir
                include "version.txt", "README*", "LICENSE*", "NOTICE*", "licenses/"
                include "config/"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            // Defaults, for clarity
            groupId = getGroup()
            artifactId = getName()
            version = getVersion()

            pom {
                name = "Aiven Kafka Connect JDBC connector"
                description = "A Kafka Connect JDBC connector for copying data between databases and Kafka."
                url = "https://aiven.io"
                organization {
                    name = "Aiven Oy"
                    url = "https://aiven.io"
                }
                licenses {
                    license {
                        name = "Apache License 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.html"
                        distribution = "repo"

                    }
                }
                scm {
                    connection = "scm:git:git://github.com/aiven/aiven-kafka-connect-jdbc.git"
                    developerConnection = "scm:git:git@github.com:aiven/aiven-kafka-connect-jdbc.git"
                    url = "https://github.com/aiven/aiven-kafka-connect-jdbc"
                    tag = "HEAD"
                }
            }
        }
    }
}

ext {
    kafkaVersion = "2.2.0"
    slf4jVersion = "1.7.25"
    confluentPlatformVersion = "4.1.4"
}

sourceSets {
    integrationTest {
        java {
            srcDirs = ['src/integration-test/java']
        }
        resources {
            srcDirs = ['src/integration-test/resources']
        }

        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntimeClasspath
}

test {
    useJUnit()
}

dependencies {
    compileOnly "org.apache.kafka:connect-api:$kafkaVersion"

    runtimeOnly "org.xerial:sqlite-jdbc:3.32.3"
    runtimeOnly "org.postgresql:postgresql:42.2.10"
    runtimeOnly "net.sourceforge.jtds:jtds:1.3.1"
    runtimeOnly "net.snowflake:snowflake-jdbc:3.12.8"
    runtimeOnly "com.microsoft.sqlserver:mssql-jdbc:8.2.1.jre11"
    runtimeOnly "mysql:mysql-connector-java:8.0.21"

    implementation "com.google.guava:guava:27.1-jre"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation "junit:junit:4.12"
    testImplementation "org.easymock:easymock:4.0.1"
    testImplementation "org.powermock:powermock-module-junit4:2.0.0"
    testImplementation "org.powermock:powermock-api-easymock:2.0.0"
    testImplementation "org.mockito:mockito-all:1.10.19"
    testImplementation "org.apache.kafka:connect-api:$kafkaVersion"
    testImplementation "commons-io:commons-io:2.6"
    testImplementation "org.apache.derby:derby:10.11.1.1"
    testRuntime "org.slf4j:slf4j-log4j12:$slf4jVersion"

    integrationTestImplementation "org.apache.kafka:connect-api:$kafkaVersion"
    integrationTestImplementation "org.apache.kafka:connect-runtime:$kafkaVersion"
    integrationTestImplementation "org.apache.kafka:connect-json:$kafkaVersion"
    integrationTestImplementation "org.apache.kafka:connect-transforms:$kafkaVersion"
    integrationTestImplementation "io.confluent:kafka-avro-serializer:$confluentPlatformVersion"
    integrationTestImplementation "io.confluent:kafka-connect-avro-converter:$confluentPlatformVersion"
    integrationTestImplementation "org.apache.avro:avro:1.8.1"

    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'

    integrationTestImplementation "org.testcontainers:junit-jupiter:1.14.3"
    integrationTestImplementation "org.testcontainers:testcontainers:1.14.3"
    integrationTestImplementation "org.testcontainers:kafka:1.14.3"
    integrationTestImplementation "org.testcontainers:postgresql:1.14.3"

    integrationTestImplementation "cloud.localstack:localstack-utils:0.2.5"

    integrationTestImplementation sourceSets.test.output
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    dependsOn test, distTar

    useJUnitPlatform()

    // Run always.
    outputs.upToDateWhen { false }

    if (project.hasProperty("tablePollIntervalMs")) {
        //pass table.poll.interval.ms
        systemProperty("integration-test.table.poll.interval.ms", project.hasProperty("tablePollIntervalMs"))
    }

    // Pass the distribution file path to the tests.
    systemProperty("integration-test.distribution.file.path", distTar.archiveFile.get().asFile.path)
}


processResources {
    filesMatching('aiven-kafka-connect-jdbc-version.properties') {
        expand(version: version)
    }
}

task connectorConfigDoc {
    description = "Generates the connector's configuration documentation."
    group = "documentation"
    dependsOn "classes"

    doLast {
        javaexec {
            main = "io.aiven.connect.jdbc.source.JdbcSourceConnectorConfig"
            classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
            standardOutput = new FileOutputStream("$projectDir/docs/source-connector-config-options.rst")
        }
        javaexec {
            main = "io.aiven.connect.jdbc.sink.JdbcSinkConfig"
            classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
            standardOutput = new FileOutputStream("$projectDir/docs/sink-connector-config-options.rst")
        }
    }
}
